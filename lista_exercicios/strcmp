#Compara a string apontada por str1 com a string apontada por str2. 
#Esta função deve comparar o primeiro caractere de cada string. 
#Se forem iguais entre si, a função deve continuar com os pares seguintes até que os caracteres difiram entre si ou 
#até que um caractere NULL (‘\0’) seja alcançado. O retorno da função depende do valor ASCII (valor decimal do byte) dos caracteres 
#comparados de acordo com o especificado a seguir:
#---------------------------------------------------------------------------------------------------------------------------------#
#Retorno: Inteiro negativo | Situação: O primeiro caractere diferente tem um valor decimal menor em str1 do que em str2.
#Retorno: Zero             | Situação: O conteúdo de ambas as strings são iguais.
#Retorno: Inteiro positivo | Situação: O primeiro caractere diferente tem um valor decimal maior em str1 do que em str2.

#Implemente esta função de modo que o parâmetro str1 deve ser passado em $a0 e o srt2 em $a1. O retorno deve ser colocado em $v0. 
#Referência adicional: https://man.archlinux.org/man/strcmp.

.data
    pergunta1: .asciiz "Digite uma palavra: "
    str1: .space 30
    pergunta2: .asciiz "Digite mais uma palavra: "
    str2: .space 30
    resultado: .asciiz "O resultado é "

.text
.globl main

#--------Função Principal----------#
main:
    # Imprime pergunta1
    li $v0, 4
    la $a0, pergunta1
    syscall

    # Lê str1
    li $v0, 8
    la $a0, str1
    li $a1, 30
    syscall

    # Imprime pergunta2
    li $v0, 4
    la $a0, pergunta2
    syscall

    # Lê str2
    li $v0, 8
    la $a0, str2
    li $a1, 30
    syscall

    # Remove '\n' das strings
    la $a0, str1
    jal remove_linha
    la $a0, str2
    jal remove_linha

    # Chama a função strcmp entre str1 e str2
    la $a0, str1
    la $a1, str2
    jal strcmp

    # $v0 contém o resultado (positivo, negativo ou zero)
    move $t0, $v0

    # Imprime "O resultado é "
    li $v0, 4
    la $a0, resultado
    syscall

    # Imprime valor
    li $v0, 1
    move $a0, $t0
    syscall

    # Fim do programa
    li $v0, 10
    syscall

# ----------Função Strcmp----------#

strcmp:
    loop:
        lb $t1, 0($a0)  # caractere de str1
        lb $t2, 0($a1)  # caractere de str2

        beq $t1, $t2, teste #verifica se os caracteres são iguais
        sub $v0, $t1, $t2 #retorna a diferença entre os dois primeiros caracteres diferentes
        jr $ra

    teste:
        beq $t1, $zero, igual # se t1 == '\0', as strings são iguais
        addi $a0, $a0, 1
        addi $a1, $a1, 1
        j loop

    igual:
        li $v0, 0
        jr $ra
        
# $a0 = str1, $a1 = str2
# retorna em $v0: negativo, zero, positivo

# ---------- Função Remove_linha ----------#
# Substitui '\n' por '\0'
remove_linha:
    analise_loop:
        lb $t3, 0($a0)
        beq $t3, 10, found
        beqz $t3, done
        addi $a0, $a0, 1
        j analise_loop
    found:
        sb $zero, 0($a0)
    done:
        jr $ra